#include "sdl.ceu"

input void SDL_REDRAW;
input void SDL_QUIT;

var _SDL_Window&? win;
    finalize
        win = &_SDL_CreateWindow("SDL 2", _SDL_WINDOWPOS_CENTERED,
                                         _SDL_WINDOWPOS_CENTERED,
                                         800, 480,
                                         _SDL_WINDOW_SHOWN);
    with
        _SDL_DestroyWindow(&&win!);
    end

var int w, h;
_SDL_GetWindowSize(&&win!, &&w, &&h);

var _SDL_Renderer&? ren;
    finalize
        ren = &_SDL_CreateRenderer(&&win!, -1, 0);
    with
        _SDL_DestroyRenderer(&&ren!);
    end

var SDL_Rect  bg     = SDL_Rect(0,0 , w,h);
var SDL_Color bg_clr = SDL_Color(0x00, 0x00, 0x00, 0xFF);

class Rect with
    var _SDL_Renderer& ren;
    var SDL_Rect       rect;
    var int            v;
do
    var _SDL_Color clr;
        clr.r = 0xFF;
        clr.g = 0xFF;
        clr.b = 0xFF;

    par/or do
        loop do
            await (1000/this.v) ms;
            rect.x = rect.x + 1;
            if rect.x > 500 then
                break;
            end
        end
    with
        every SDL_REDRAW do
            _SDL_SetRenderDrawColor(&&ren, clr.r,clr.g,clr.b,0xFF);
            _SDL_RenderFillRect(&&ren, (_SDL_Rect&&)&&rect);
        end
    end
end

par/or do
    await SDL_QUIT;
with
    every SDL_REDRAW do
        _SDL_SetRenderDrawColor(&&ren!, bg_clr.r,bg_clr.g,bg_clr.b,0xFF);
        _SDL_RenderFillRect(&&ren!, (_SDL_Rect&&)&&bg);
    end
with
    loop do
        var Rect r1 with
            this.ren    = &ren!;
            this.rect.x = 100;
            this.rect.y = 100;
            this.rect.w = 20;
            this.rect.h = 20;
            this.v      = 100;
        end;

        var Rect r2 with
            this.ren    = &ren!;
            this.rect.x = 100;
            this.rect.y = 200;
            this.rect.w = 40;
            this.rect.h = 40;
            this.v      = 25;
        end;

        await 10s;
    end
with
    every SDL_REDRAW do
        _SDL_RenderPresent(&&ren!);
    end
end

escape 0;
