#define HEADER_TITLE "SDL - 03 (class)"
#include "header.ceu"

class Rect with
    var _SDL_Renderer&? ren;
    var _SDL_Rect r;
    var int dt;
do
    this.r.x = 100;
    this.r.w =  20;
    this.r.h =  20;

    par do
        every (this.dt)ms do
            this.r.x = this.r.x + 1;
        end
    with
        every SDL_REDRAW do
            _SDL_SetRenderDrawColor(&this.ren, 0xFF,0xFF,0xFF,0xFF);
            _SDL_RenderFillRect(&this.ren, &r);
        end
    end
end

par/or do
    every SDL_REDRAW do
        _SDL_SetRenderDrawColor(_REN, 0x00,0x00,0x00, 0xFF);
        _SDL_RenderFillRect(_REN, null);
    end
with
    var Rect r1 with
        this.ren = _REN;
        this.r.y = 100;
        this.dt  =  15;
    end;
    var Rect r2 with
        this.ren = _REN;
        this.r.y = 300;
        this.dt  =  10;
    end;
    await FOREVER;
with
    every SDL_REDRAW do
        _SDL_RenderPresent(_REN);
    end
with
    await SDL_QUIT;
end

escape 0;
