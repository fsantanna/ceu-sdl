#ifndef ALL

#include "sdl.ceu"
#include "sdl-gfx.ceu"

input void SDL_REDRAW;
input void SDL_QUIT;

var int win_w;
var int win_h;
var _SDL_Window&? win;
    finalize
        win = &_SDL_CreateWindow("(3) Hello World!",
                _SDL_WINDOWPOS_UNDEFINED, _SDL_WINDOWPOS_UNDEFINED,
                800, 480, _SDL_WINDOW_SHOWN);
    with
        _SDL_DestroyWindow(&&win!);
    end

_SDL_GetWindowSize(&&win!, &&win_w, &&win_h);

var _SDL_Renderer&? ren;
    finalize
        ren = &_SDL_CreateRenderer(&&win!, -1, 0);
    with
        _SDL_DestroyRenderer(&&ren!);
    end

#else

_SDL_SetWindowTitle(&&win!, "(3) Hello World!");

#endif

var bool is_on = false;

par/or do
    await SDL_QUIT;
with
    loop do
        par/or do
            loop do
                await 500ms;
                is_on = not is_on;
            end
        with
            await 5s;
        end
        await 2s;
    end
with
    every SDL_REDRAW do
        _boxRGBA(&&ren!, 0,0,win_w,win_h, 0xCC,0xCC,0xCC,0xFF);
        _filledCircleRGBA(&&ren!, 200,200,50, 0xFF,0xFF,0xFF,0xFF);
        if is_on then
            _filledCircleRGBA(&&ren!, 200,200,47, 0x00,0x88,0x00,0xFF);
        else
            _filledCircleRGBA(&&ren!, 200,200,47, 0x00,0x00,0x00,0xFF);
        end
        _SDL_RenderPresent(&&ren!);
    end
end

escape 0;
