#include "sdl.ceu"
#include "sdl-gfx.ceu"

input void SDL_REDRAW;
input void SDL_QUIT;

native do
    SDL_Renderer* REN = NULL;
    int REN_W, REN_H;
end

var _SDL_Window&? win;
    finalize
        win = &_SDL_CreateWindow("SDL 7", _SDL_WINDOWPOS_CENTERED,
                                         _SDL_WINDOWPOS_CENTERED,
                                         800, 480,
                                         _SDL_WINDOW_SHOWN);
    with
        _SDL_DestroyWindow(&&win!);
    end

_SDL_GetWindowSize (&&win!, &&_REN_W, &&_REN_H);

finalize
    _REN = _SDL_CreateRenderer(&&win!, -1, 0);
with
    _SDL_DestroyRenderer(_REN);
end

var SDL_Rect  bg     = SDL_Rect(0,0, _REN_W,_REN_H);
var SDL_Color bg_clr = SDL_Color(0xFF,0xFF,0x00,0xFF);

var SDL_Rect  fg     = SDL_Rect(100,100, 100,100);
var SDL_Color fg_clr = SDL_Color(0xFF,0x00,0x00,0xFF);

par/or do
    await SDL_QUIT;
with
    every 1s do
        fg.x = fg.x + 10;
        fg.y = fg.y + 10;
    end
with
    every SDL_REDRAW do
        _SDL_SetRenderDrawColor(_REN, bg_clr.r,bg_clr.g,bg_clr.b,0xFF);
        _SDL_RenderFillRect(_REN, (_SDL_Rect&&)&&bg);

        _SDL_SetRenderDrawColor(_REN, fg_clr.r,fg_clr.g,fg_clr.b,0xFF);
        _SDL_RenderFillRect(_REN, (_SDL_Rect&&)&&fg);

        loop i in 30 do
            _pixelRGBA(_REN, 10+_rand()%50, 10+_rand()%50, 100,100,100, 0xFF);
        end

        native do
            short s[5] = { 500, 550, 650, 525, 400 };
            short t[5] = { 200, 210, 250, 225, 300 };
        end

        _filledPolygonRGBA(_REN, _s, _t, 5, 0x00, 0xFF,0x00, 0xFF);

        _boxRGBA(_REN, 500,200, 550,250, 0x00, 0x00, 0xFF, 0xBB);
        _roundedBoxRGBA(_REN, 520,220, 570,270, 4, 0x00, 0xFF, 0x00, 0xBB);
        _roundedRectangleRGBA(_REN, 540,240, 590,290, 8, 0xFF, 0x00, 0x00,0x99);

        _SDL_RenderPresent(_REN);
    end
end

escape 0;
